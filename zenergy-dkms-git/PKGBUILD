# Maintainer: bouhaa <boukehaarsma23 at gmail dot com>
# Maintainer: omarhanykasban <omarhanykasban706@gmail.com>

_pkgname=zenergy
pkgname=$_pkgname-dkms-git
pkgver=21.09f4d2f
pkgrel=2
pkgdesc='Linux kernel driver for reading RAPL registers for AMD Zen CPUs with patch for linux 6.11'
arch=('x86_64' 'i686')
url='https://github.com/boukehaarsma23/zenergy'
license=('GPL2')
depends=('dkms')
provides=('zenergy-dkms')
source=("$_pkgname::git+$url.git"
        "https://github.com/BoukeHaarsma23/zenergy/pull/11.patch")
b2sums=('SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_pkgname"
  # Apply the patch from PR 11 for kernel 6.5 support
  patch -Np1 -i "$srcdir/11.patch"
  
  sed -e "s/@CFLGS@//" \
      -e "s/@VERSION@/$pkgver/" \
      -i dkms.conf
}

package() {
  install -Dm644 "$srcdir/$_pkgname/dkms.conf" "$pkgdir/usr/src/$_pkgname-$pkgver/dkms.conf"
  install -Dm644 "$srcdir/$_pkgname/Makefile" "$pkgdir/usr/src/$_pkgname-$pkgver/Makefile"
  install -Dm644 "$srcdir/$_pkgname/zenergy.c" "$pkgdir/usr/src/$_pkgname-$pkgver/zenergy.c"
}
